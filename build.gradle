
plugins {
    id 'com.jfrog.bintray' version '1.7.3'
    id 'net.ltgt.errorprone' version '0.0.16'
}

allprojects {
    apply plugin: 'maven-publish'
    group = 'nl.weeaboo.gdx-styledtext'
    version = '4.0.1'

    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/anonl/nvlist/" }
    }
}

ext.errorProneVersion = '2.3.3'

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    ext {
        javaVersion = JavaVersion.VERSION_1_6

        gdxVersion = '1.9.6'
        junitVersion = '4.12'
        gdxTestVersion = '2.0.1'
        guavaVersion = '20.0';

        pomLicenseConfig = {
           licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
        pomLicenseConfig.resolveStrategy = Closure.DELEGATE_ONLY // Otherwise 'license' elem is missing
    }
    
    sourceCompatibility = javaVersion
    
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    
    dependencies {
        // ErrorProne annotations
        compileOnly "com.google.errorprone:error_prone_annotations:${rootProject.ext.errorProneVersion}"
        // JSR305 annotations (nullable, etc.)
        compileOnly "com.google.code.findbugs:jsr305:3.0.1"

        testCompile "junit:junit:$junitVersion"
        testCompile "nl.weeaboo.gdx-test:gdx-test-core:$gdxTestVersion"
        testCompile "com.google.guava:guava-testlib:$guavaVersion"
    }
    
    test {
        testLogging {
            info {
                events 'skipped', 'passed', 'failed'
                showStandardStreams true
            }
        }
        
        def uiTest = project.hasProperty('uiTest')
        inputs.property('uiTest', uiTest) // Re-execute task if uiTest property changes        
        useJUnit {
            if (!uiTest) {            
                // Exclude UI tests by default
                excludeCategories 'nl.weeaboo.gdx.test.junit.GdxUiTest'
            }
        }        
    }
    
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }
    
    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    // ErrorProne compiler isn't compatible with Java 6, so check that the current JVM uses at least Java 7
    if (JavaVersion.current().isJava7Compatible()) {
        apply from: rootProject.file('errorprone.gradle')
    }
}

project(":styledtext-api") {    
}

project(":styledtext-impl") {
    dependencies {
        compile project(':styledtext-api')
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"

        testCompile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        testRuntime "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        testRuntime "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    }
}

publishing {
    publications {
        api(MavenPublication) {
            artifactId = 'styledtext-api'

            def proj = project(":styledtext-api")
            from proj.components.java            
            artifact(proj.sourceJar)            

            pom.withXml {
                asNode().children().last() + proj.ext.pomLicenseConfig
            }  
        }
        impl(MavenPublication) {
            artifactId = 'styledtext-impl'

            def proj = project(":styledtext-impl")
            from proj.components.java            
            artifact(proj.sourceJar)            

            pom.withXml {
                asNode().children().last() + proj.ext.pomLicenseConfig
            }  
        }
    }
}

bintray {    
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['api', 'impl']
    pkg {
        repo = 'nvlist'
        name = 'gdx-styledtext'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/anonl/gdx-styledtext.git'
        version {
            name = project.version
        }
    }
}

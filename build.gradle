
plugins {
    id "com.jfrog.bintray" version "1.6"
}

allprojects {
    apply plugin: "maven-publish"
    group = 'nl.weeaboo.gdx-styledtext'
    version = '1.5.4'
    
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://dl.bintray.com/anonl/gdx/" }
    }    
}

subprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "java"
    
    ext {
        gdxVersion = '1.9.3'
        junitVersion = '4.12'
        gdxTestVersion = '1.0.4'
    }
    
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    
    dependencies {
        testCompile "junit:junit:$junitVersion"
        testCompile "nl.weeaboo.gdx-test:gdx-test-core:$gdxTestVersion"
    }
    
    test {
        // jvmArgs '-Xcheck:jni'
        // jvmArgs '-XX:OnError=cat hs_err_pid%p.log'
        
        testLogging {
            info {
                events 'skipped', 'passed', 'failed'
                showStandardStreams true
            }
        }
        
        def uiTest = project.hasProperty('uiTest')
        inputs.property('uiTest', uiTest) // Re-execute task if uiTest property changes        
        useJUnit {
            if (!uiTest) {            
                // Exclude UI tests by default
                excludeCategories 'nl.weeaboo.gdx.test.junit.GdxUiTest'
            }
        }        
    }
    
    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
        classifier "sources"
    }
}

project(":styledtext-api") {    
}

project(":styledtext-impl") {
    dependencies {
        compile project(':styledtext-api')
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"        
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"        

        testCompile "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        testRuntime "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        testRuntime "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    }
}

publishing {
    publications {
        api(MavenPublication) {
            artifactId = 'styledtext-api'

            def proj = project(":styledtext-api")
            from proj.components.java            
            artifact(proj.sourceJar)            
        }
        impl(MavenPublication) {
            artifactId = 'styledtext-impl'

            def proj = project(":styledtext-impl")
            from proj.components.java            
            artifact(proj.sourceJar)            
        }
    }
}

bintray {    
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['api', 'impl']
    pkg {
        repo = 'gdx-styledtext'
        name = 'gdx-styledtext'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/anonl/gdx-styledtext.git'
        version {
            name = project.version
        }
    }
}


plugins {
    id "com.jfrog.bintray" version "1.2"
}

allprojects {
    apply plugin: "maven-publish"
    group = 'nl.weeaboo.gdx-styledtext'
    version = '1.1'
}

subprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    
    ext {
        gdxVersion = '1.7.2'
        junitVersion = '4.12'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }    
}

project(":styledtext-api") {
    apply plugin: "java"
    
    sourceCompatibility = 1.6
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    
    dependencies {
    	testCompile "junit:junit:$junitVersion"
    }
}

project(":styledtext-impl") {
    apply plugin: "java"
    
    sourceCompatibility = 1.6
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
    
    dependencies {
        compile project(':styledtext-api')
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"        
        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"        

    	testCompile "junit:junit:$junitVersion"
        testCompile "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
		testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}

publishing {
    publications {
        api(MavenPublication) {
            from project(":styledtext-api").components.java
            artifactId = 'styledtext-api'
        }
        impl(MavenPublication) {
            from project(":styledtext-impl").components.java
            artifactId = 'styledtext-impl'
        }
    }
}

bintray {	
	user = System.getenv('BINTRAY_USER')
	key = System.getenv('BINTRAY_KEY')
	publications = ['api', 'impl']
	pkg {
		repo = 'gdx-styledtext'
		name = 'gdx-styledtext'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/anonl/gdx-styledtext.git'
        version {
            name = project.version
        }
    }
}
